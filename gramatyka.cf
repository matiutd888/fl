-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

-- [M] dodatek, zmienne globalne.
-- GlobalDef. TopDef ::= Decl;

-- [M] Wyrażenia lambda.
Lambda. Lambda ::= "lambda:" "(" [Arg] ")" "->" Type Block;

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type Ident;

-- [M] Referencje.

ArgRef.    Arg ::= Type "ref" Ident;


separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Decl ::= Type [Item] ";" ;

-- [M] dodatek, zmienne niemutowalne

ConstDecl. Decl ::= "const" Type [Item] ";";
 
FDecl.     Decl ::= Type Ident "(" [Arg] ")" Block ";" ;

DeclStmt.  Stmt ::=  Decl;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

-- [M] przypisywanie do krotek

TupleIdentDupa. TupleIdent ::= Ident;
TupleRec. TupleIdent ::= "<" [TupleIdent] ">";

separator nonempty TupleIdent ",";

-- separator nonempty Ident ",";

TupleAss.       Stmt ::= "<" [ TupleIdent ] ">" "=" Expr  ";" ;

Ass.    Stmt ::= Ident "=" Expr ";" ;

-- Incr.      Stmt ::= Ident "++"  ";" ;

-- Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" "{" Stmt  "}";


CondElse.  Stmt ::= "if" "(" Expr ")" "{" Stmt "}" "else" "{" Stmt "}"  ;

-- [M] dodatek, e else ifa

-- Cond.      Stmt ::= "if" "(" Expr ")" "{" Stmt  "}" Elif;

-- Elif.    Elif ::= "elif" "(" Expr ")" "{" Stmt "}" Elif ; 

-- Else.    Elif ::= "else" "{" Stmt "}";

-- EmptyIf.       Elif ::= "";


While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "bool" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

-- [M] Tuple.

Tuple.     Type ::= "tuple" "<" [Type] ">" ;

-- [M] Typ funkcji.

Function. Type ::= "function" "<" Type "(" [Type] ")" ">";

separator  Type "," ;

-- Expressions ---------------------------------------------

-- TODO dodać get value by index

ETuple. Expr6 ::= "[" [Expr] "]" ;

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ELambda.    Expr6 ::= Lambda;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;


coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- [M] pomysł, podnoszenie do potęgi
POW.       RelOp ::= "^";
-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
